apply plugin: 'jacoco'
apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'com.github.dcendents.android-maven'

project.archivesBaseName = 'widget-sample'

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId "com.soprasteria.movalysmdk.widget.sample"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName

        testInstrumentationRunner "com.soprasteria.movalysmdk.espresso.AndroidJacocoTestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    jacoco {
        //bug in code coverage with espresso 2.1:
        //https://code.google.com/p/android/issues/detail?id=170607
        version jacocoVersion
    }

    packagingOptions {
        exclude 'META-INF/LICENCE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':core')
    compile project(':basic')
    compile project(':position')
    compile supportDependencies.support
    compile supportDependencies.appCompat
    //to run test: ./gradlew connectedAndroidTest
    androidTestCompile supportDependencies.supportAnnotations
    androidTestCompile testDependencies.espressoCore
    androidTestCompile(testDependencies.espressoContrib) {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile testDependencies.testRunner
    androidTestCompile testDependencies.spoonClient
    androidTestCompile testDependencies.mdkEspresso
    androidTestCompile(project(':espresso')) {
        transitive = false
    }
    compile project(':position')
}

sonarRunner {
        sonarProperties {
                properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
                properties["sonar.sources"] += "src/androidTest/java"
                //properties["sonar.tests"] = "src/androidTest/java"
                properties["sonar.binaries"] = file("build/intermediates/classes/debug")
                properties["sonar.junit.reportsPath"] = file("build/outputs/androidTest-results/connected/")
        }
}

//to run spoon: ./gradlew spoon
spoon {
    // for debug output
    debug = true

    // To run a single test class
    //className = 'fully.qualified.TestCase'

    // To run a single method in TestCase
    //methodName = 'testMyApp'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mdkArtifactoryRepoReleases) {
                authentication(userName: mdkArtifactoryUsername, password: mdkArtifactoryPassword)
            }
            snapshotRepository(url: mdkArtifactoryRepoSnapshots) {
                authentication(userName: mdkArtifactoryUsername, password: mdkArtifactoryPassword)
            }
        }
    }
}

