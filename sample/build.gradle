apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'com.github.dcendents.android-maven'

project.archivesBaseName = 'widget-sample'

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId "com.soprasteria.movalysmdk.widget.sample"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/LICENCE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':core')
    compile project(':basic')
    compile project(':position')
    compile project(':positionmaps')
    compile project(':spinner')
    compile project(':media')
    compile supportDependencies.support
    compile supportDependencies.appCompat
    compile supportDependencies.recyclerView
    compile googleApis.mapsServices
    compile googleApis.locationServices
    //to run test: ./gradlew connectedAndroidTest
    androidTestCompile supportDependencies.supportAnnotations
    androidTestCompile testDependencies.espressoCore
    androidTestCompile(testDependencies.espressoContrib) {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile testDependencies.espressoIntents
    androidTestCompile testDependencies.testRunner
    androidTestCompile testDependencies.spoonClient
    androidTestCompile testDependencies.mdkEspresso
    androidTestCompile testDependencies.intentsClient
    androidTestCompile(project(':espresso')) {
        transitive = false
    }
    compile project(':fixedlist')
}

//to run spoon: ./gradlew spoon
spoon {
    // for debug output
    debug = true

    // To run a single test class
    //className = 'fully.qualified.TestCase'

    // To run a single method in TestCase
    //methodName = 'testMyApp'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mdkArtifactoryRepoReleases) {
                authentication(userName: mdkArtifactoryUsername, password: mdkArtifactoryPassword)
            }
            snapshotRepository(url: mdkArtifactoryRepoSnapshots) {
                authentication(userName: mdkArtifactoryUsername, password: mdkArtifactoryPassword)
            }
        }
    }
}

