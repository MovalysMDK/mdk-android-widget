// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath 'com.squareup.spoon:spoon-runner:1.2.1'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:1.0.3') {
            exclude module: "guava"
        }
//        classpath 'com.google.guava:guava:17.0'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'org.jacoco:org.jacoco.core:0.7.5.201505241946'
    }
}

plugins {
  id "org.sonarqube" version "1.1"
}

// sonar
apply from: 'sonar.gradle'

apply plugin: 'license'

// Load dependencies
apply from: 'dependencies.gradle'

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

allprojects {

    repositories {
        mavenLocal()

        maven {
            url mdkArtifactoryRepoReleases
            credentials {
                username mdkArtifactoryUsername
                password mdkArtifactoryPassword
            }
        }
        maven {
            url mdkArtifactoryRepoSnapshots
            credentials {
                username mdkArtifactoryUsername
                password mdkArtifactoryPassword
            }
        }
    	jcenter()
    }

    group = 'com.soprasteria.movalysmdk.widget'
    version = '1.6.0-SNAPSHOT'

    ext {
        androidBuildToolsVersion = "24.0.2"
        androidMinSdkVersion = 15
        androidTargetSdkVersion = 24
        androidCompileSdkVersion = 24

        androidVersionCode = 101100
        androidVersionName = '1.6.0'
    }
}

license {
    strictCheck true
    //exclude "**/*.properties"
    //excludes(["**/*.txt", "**/*.conf"])
    //include "**/*.groovy"
    //includes(["**/*.java", "**/*.properties"])
}

// compilation options
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
    options.debug = true
}

task mdkLicenseFormat (type:nl.javadude.gradle.plugins.license.License) {
    source = [
        fileTree(dir:"core/src/main/java").include("**/*.java"),
        fileTree(dir:"core/src/main/res").include("**/*.xml"),
        fileTree(dir:"core/src/main").include("AndroidManifest.xml"),
        fileTree(dir:"basic/src/main/java").include("**/*.java"),
        fileTree(dir:"basic/src/main/res").include("**/*.xml"),
        fileTree(dir:"basic/src/main").include("AndroidManifest.xml"),
        fileTree(dir:"sample/src/main/java").include("**/*.java"),
        fileTree(dir:"sample/src/main/res").include("**/*.xml"),
        fileTree(dir:"sample/src/main").include("AndroidManifest.xml"),
        fileTree(dir:"sample/src/androidTest/java").include("**/*.java")
    ]
}

// Run ./gradlew licenseFormat
licenseFormat.dependsOn mdkLicenseFormat
clean.dependsOn licenseFormat

